{
  parserClass="org.legendofdragoon.fatescript.psi.FateScriptParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="FateScript"
  psiImplClassSuffix="Impl"
  psiPackage="org.legendofdragoon.fatescript.psi"
  psiImplPackage="org.legendofdragoon.fatescript.psi.impl"

  elementTypeHolderClass="org.legendofdragoon.fatescript.psi.FateScriptTypes"
  elementTypeClass="org.legendofdragoon.fatescript.psi.FateScriptElementType"
  tokenTypeClass="org.legendofdragoon.fatescript.psi.FateScriptTokenType"

  tokens=[
    eol='regexp:\n+'
    space='regexp:\s+'
    comment='regexp:;.*'
    dec='regexp:\d+'
    id='regexp:[a-zA-Z_]\w*'
    hex='regexp:0x[a-fA-F\d]+'
  ]

//  name(".*expr")='expression'
//  extends(".*expr")=expr
}

root ::= line *
private line ::= !<<eof>> (code | label)? eol+ {pin=2}

code ::= op !":"
label ::= id ":"
op ::= id params?
params ::= param (("," param)+)?
param ::= methodRef | number | inline | jump | var | storage | cmp | id
methodRef ::= id "::" id
number ::= dec | hex
inline ::= "inl" inlineIndex
jump ::= ":" id
jumpTable ::= jump (jumpTable | index)
inlineIndex ::= "[" (inlineIndex | jumpTable | jump) "]"
index ::= "[" (dec | storage "," dec | storage | jumpTable) ("+" dec)? "]"
var ::= "var" index+
storage ::= "stor" index
cmp ::= "<=" | "<" | "==" | "!=" | ">" | ">=" | "&" | "!&"
