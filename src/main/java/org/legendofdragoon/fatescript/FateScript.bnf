{
  parserClass="org.legendofdragoon.fatescript.psi.FateScriptParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="FateScript"
  psiImplClassSuffix="Impl"
  psiPackage="org.legendofdragoon.fatescript.psi"
  psiImplPackage="org.legendofdragoon.fatescript.psi.impl"

  elementTypeHolderClass="org.legendofdragoon.fatescript.psi.FateScriptTypes"
  elementTypeClass="org.legendofdragoon.fatescript.psi.FateScriptElementType"
  tokenTypeClass="org.legendofdragoon.fatescript.psi.FateScriptTokenType"

  tokens=[
    eol='regexp:\n'
    space='regexp:\s+'
    comment='regexp:;.*'
    dec='regexp:\d+'
    hex='regexp:[a-fA-F\d]{6}'
    fullhex='regexp:0x[a-fA-F\d]+'
    id='regexp:[a-zA-Z_]\w*'
    string="regexp:('([^'\\]|\\.)*'|\"([^\"\\]|\\.)*\")"
  ]

//  name(".*expr")='expression'
//  extends(".*expr")=expr
}

root ::= root_item *
private root_item ::= !<<eof>> line

line ::= (code | label) {pin=1 recoverWhile=lineRecover}
private lineRecover ::= !(code | label)

code ::= lineNumber? op !":"
label ::= id ":"
lineNumber ::= dec | hex
op ::= id params?
params ::= param (("," param)+)?
param ::= methodRef | number | inline | jump | var | storage
methodRef ::= id "::" id
number ::= dec | fullhex
inline ::= "inl" inlineIndex
jump ::= ":" id
jumpTable ::= jump (jumpTable | index)
inlineIndex ::= "[" (inlineIndex | jumpTable | jump) "]"
index ::= "[" (dec | storage "," dec | storage | jumpTable) ("+" dec)? "]"
var ::= "var" index+
storage ::= "stor" index
